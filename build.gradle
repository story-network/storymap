/*
 * Created on Sat Sep 26 2020
 *
 * Copyright (c) storycraft. Licensed under the MIT Licence.
 */

import com.github.jengelman.gradle.plugins.shadow.tasks.ConfigureShadowRelocation

plugins {
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

// Info
group 'sh.pancake'
version '1.0.0'

sourceCompatibility = 1.9
targetCompatibility = 1.9

compileJava.options.encoding = 'UTF-8'

repositories {
    jcenter()

    maven {
        url = 'https://maven.heartpattern.io/repository/maven-public/'
    }
}

configurations {
    implementation.extendsFrom(embed)
}

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    embed project(':story-network-common')

    embed 'io.heartpattern:mcremapper:2.0.2-SNAPSHOT'
    embed 'com.google.code.gson:gson:2.8.6'
}
configurations.compile.dependencies.remove dependencies.gradleApi()

shadowJar {

    configurations = [project.configurations.embed]

}

task relocateShadowJar(type: ConfigureShadowRelocation) {
    target = tasks.shadowJar
    prefix = "sh.pancake.lib"
}
tasks.shadowJar.dependsOn tasks.relocateShadowJar

jar {

    finalizedBy shadowJar
    
}

gradlePlugin {
    plugins {
        create('StoryMap') {
            id = 'sh.pancake.storymap'
            implementationClass = 'sh.pancake.storymap.StoryMapPlugin'
        }
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }

        resources {
            srcDirs files('src/main/resources')
        }
    }
}